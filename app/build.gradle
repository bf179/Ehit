plugins {
    id 'com.android.application'
    id 'kotlin-android'

    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'

    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.mitsuki.ehit"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias = 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        //debug info
        def properties = new Properties()
        def inputStream = project.rootProject.file('local.properties').newDataInputStream()
        properties.load(inputStream)

        debug {
            buildConfigField "String", "ipb_member_id", properties.getProperty('ipb_member_id')
            buildConfigField "String", "ipb_pass_hash", properties.getProperty('ipb_pass_hash')
            buildConfigField "String", "igneous", properties.getProperty('igneous')
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "String", "ipb_member_id", '""'
            buildConfigField "String", "ipb_pass_hash", '""'
            buildConfigField "String", "igneous", '""'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {
        dev {
            applicationIdSuffix '.dev'
            dimension "version"
            manifestPlaceholders = [
                    appName: "Ehit.",
            ]
        }

        official {
            dimension "version"
            manifestPlaceholders = [
                    appName: "Ehit",
            ]
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        /**
         * The kotlinx-coroutines-core artifact contains a resource file that is not required for the coroutines to operate normally and is only used by the debugger.
         */
        resources.excludes += "DebugProbesKt.bin"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //----------------------------------------------------------------------------------------------
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0' //协程

    //noinspection GradleDynamicVersion
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //----------------------------------------------------------------------------------------------
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.biometric:biometric:1.2.0-alpha04'

    //----------------------------------------------------------------------------------------------
    //hilt
    implementation("com.google.dagger:hilt-android:2.38.1")
    kapt("com.google.dagger:hilt-android-compiler:2.38.1")
    //viewmodel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
    //LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"
    //paging3
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'
    //navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.2"
    //room
    implementation "androidx.room:room-runtime:2.4.1"
    kapt "androidx.room:room-compiler:2.4.1"
    implementation "androidx.room:room-ktx:2.4.1"

    //----------------------------------------------------------------------------------------------

    implementation "org.jsoup:jsoup:1.13.1"

    implementation "com.squareup.okhttp3:okhttp:4.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"

    implementation "io.coil-kt:coil:2.0.0-rc03"

    implementation "com.squareup.moshi:moshi-kotlin:1.12.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.12.0"

    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"

    //----------------------------------------------------------------------------------------------
    implementation platform('com.google.firebase:firebase-bom:28.0.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'

    //----------------------------------------------------------------------------------------------
    implementation "com.mitsuki.armory:base:1.0.2"
    implementation "com.mitsuki.armory:httprookie:1.0.2"
    implementation "com.mitsuki.armory:loadprogress:1.0.0"
    implementation "com.mitsuki.armory:adapter:1.0.3"
    implementation "com.mitsuki.armory:imagegesture:1.0.0"
    implementation "com.mitsuki.armory:systemoverlay:1.0.1"

}
